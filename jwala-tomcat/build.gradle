description "This module generates and deploys Jwala binaries. This layes creates the zip file which can be distributed and published to a repository for releases"

apply plugin: 'de.undercouch.download'
apply plugin: 'maven-publish'

import org.apache.tools.ant.filters.FixCrLfFilter
import de.undercouch.gradle.tasks.download.Download
import groovy.sql.Sql

configurations {
    compile {
        description = 'compile classpath'
        transitive = false
    }
    jdbcdriver
}

dependencies {
    jdbcdriver "com.h2database:h2:$project.versions.h2"

    compile(group: 'commons-collections', name: 'commons-collections', version: '3.2.2')
    compile(group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1')
    compile(group: 'commons-lang', name: 'commons-lang', version: '2.6')
    compile(group: 'commons-logging', name: 'commons-logging', version: '1.1.1')
    compile(group: 'org.owasp.esapi', name: 'esapi', version: '2.0.1')
    compile(group: 'org.apache.geronimo.specs', name: 'geronimo-jpa_2.0_spec', version: '1.1')
    compile(group: "com.h2database", name: "h2", version: "$project.versions.h2")
    compile(group: 'joda-time', name: 'joda-time', version: '2.2')
    compile(group: 'javax.servlet', name: 'jstl', version: '1.2')
    compile(group: 'javax.transaction', name: 'jta', version: '1.1')
    compile(group: 'log4j', name: 'log4j', version: '1.2.17')
    compile(group: 'ca.juliusdavies', name: 'not-yet-commons-ssl', version: '0.3.9')
    compile(group: 'org.opensaml', name: 'opensaml', version: '2.6.4')
    compile(group: 'org.opensaml', name: 'openws', version: '1.5.4')
    compile(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.6')
    compile(group: 'taglibs', name: 'standard', version: '1.1.2')
    compile(group: 'org.apache.santuario', name: 'xmlsec', version: '1.5.6')
    compile(group: 'org.opensaml', name: 'xmltooling', version: '1.4.4')
    compile group: 'joda-time', name: 'joda-time', version: "$project.versions.jodaTime"
    compile group: 'org.jgroups', name: 'jgroups', version: "$project.versions.jgroups"

}

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'de.undercouch:gradle-download-task:3.1.1' }
}

task deleteStagingDir(type: Delete) {
    delete "$buildDir/${'apache.tomcat.dir'}"
}

task downloadDeploymentBinaries(dependsOn: deleteStagingDir, type: Download) {
    src([project.property('jdk.binary.url'), project.property('apache.httpd.binary.url'), project.property('apache.tomcat.binary.url'), project.property('unzip.exe.binary.url')])
    dest "$buildDir/${'apache.tomcat.dir'}/data/binaries"
}

task downloadDeploymentBinariesLinux(dependsOn: deleteStagingDir, type: Download) {
    src([project.property('jdk.binary.url'), project.property('apache.httpd.binary.url'), project.property('apache.tomcat.binary.url')])
    dest "$buildDir/${'apache.tomcat.dir'}/data/binaries"
}

task downloadTomcat(type: Download) {
    if (project.hasProperty("zipOs") && zipOs == "linux") {
        dependsOn downloadDeploymentBinariesLinux
    } else {
        dependsOn downloadDeploymentBinaries
    }
    src project.property('apache.tomcat.binary.url')
    dest new File(buildDir, new File(project.property('apache.tomcat.binary.url')).getName())
}

task downloadAndUnzipTomcat(dependsOn: downloadTomcat, type: Copy) {
    from zipTree(downloadTomcat.dest)
    into buildDir
}

task deleteDownloadedZip(dependsOn: downloadAndUnzipTomcat, type: Delete) {
    delete new File("$buildDir", new File(project.property('apache.tomcat.binary.url')).getName())
    delete new File("$buildDir/${'apache.tomcat.dir'}", 'webapps')
}

task copyConfigurationOverrides(dependsOn: deleteDownloadedZip, type: Copy) {
    from('src/main/resources') {
        filter { it.replaceAll('@@buildVersion@@', "$buildVersion") }
    }

    from('src/main/resources') {
        include '**/*.sh'
        include '**/*/*.sh'
        filter(FixCrLfFilter.class,
                eol: FixCrLfFilter.CrLf.newInstance("lf")) //convert to unix format
    }

    into("$buildDir/${'apache.tomcat.dir'}")
}

task deleteReadMeFiles(dependsOn: copyConfigurationOverrides, type: Delete) {
    delete fileTree("$buildDir/${'apache.tomcat.dir'}") {
        include '**/ReadMe.txt'
    }
}

task copyTomcatExtras(dependsOn: deleteReadMeFiles, type: Download) {
    src 'http://archive.apache.org/dist/tomcat/tomcat-7/v7.0.55/bin/extras/catalina-jmx-remote.jar'
    dest new File("$buildDir/${'apache.tomcat.dir'}/lib", "catalina-jmx-remote.jar")
}

task copyJwalaDependenciesToTomcatLib(dependsOn: copyTomcatExtras, type: Copy) {
    from configurations.compile
    into("$buildDir/${'apache.tomcat.dir'}/lib")
}

task copyJwalaH2EmbededJar(dependsOn: copyJwalaDependenciesToTomcatLib, type: Copy) {
    project(':jwala-h2-embedded') {
        dependsOn(build)
    }
    from project(':jwala-h2-embedded').file("build/libs")
    into file("$buildDir/${'apache.tomcat.dir'}/lib")
}
task copyJwalaAgent(dependsOn: copyJwalaH2EmbededJar, type: Copy) {
    project(':jwala-tomcat-agent') {
        dependsOn(build)
    }
    from(project(':jwala-tomcat-agent').file("build/libs"))
    from(file("$buildDir/${'apache.tomcat.dir'}/lib/")) {
        include "$jgroups_lib"
        include "$slf4j_lib"
        include "$joda_time_lib"
    }
    into file("$buildDir/${'apache.tomcat.dir'}/data/agent")
}

task copyJwalaWebappWar(dependsOn: copyJwalaAgent, type: Copy) {
    project(':jwala-webapp') {
        dependsOn(build)
    }
    from project(':jwala-webapp').file("build/libs")
    into file("$buildDir/${'apache.tomcat.dir'}/data/webapps")
}
task copySql(dependsOn: [':jwala-persistence:createDdl', copyJwalaWebappWar], type: Copy) {
    from project(':jwala-persistence').projectDir
    include 'create.sql'
    include 'update.sql'
    into file("$buildDir/${'apache.tomcat.dir'}/data/db")
}

task createJwalaDb(dependsOn: copySql) << {

    configurations.jdbcdriver.files.each {
        Sql.classLoader.addURL(it.toURI().toURL())
    }

    def sql = Sql.newInstance(
            "jdbc:h2:file://$buildDir/${'apache.tomcat.dir'}/data/db/jwala;LOCK_MODE=0",
            'sa',
            '',
            'org.h2.Driver')

    def dbFile = file("$buildDir/${'apache.tomcat.dir'}/data/db/jwala.h2.db")
    if (dbFile.exists()) {
        sql.execute('drop all objects')
    }
    sql.execute(file("$buildDir/${'apache.tomcat.dir'}/data/db/create.sql").text)
    sql.close()
}

task zipJwalaTomcat(dependsOn: createJwalaDb, type: Zip) {
    from "$buildDir/${'apache.tomcat.dir'}"
    if (project.hasProperty("zipOs") && zipOs == "linux") {
        baseName = 'jwala-tomcat-linux'
    } else {
        baseName = 'jwala-tomcat'
    }
    into "${'apache.tomcat.dir'}"// note that this specifies path *in* the archive
    destinationDir file("$buildDir") // directory that you want your archive to be placed in
}